// standard reshufflings that implement pipelining
import "channel.act";
import "test.act";

namespace buffer {
    export
    template <pint N>
    defproc PCHB (globals g; e1of<N> l, r) {
        // pre-charge half-buffer
        bool vr, vl, _vl; // v/n detect l and r
        c1of<N> _r;
        prs<g.Vdd, g.GND> {
            (: n : N :
                r.e & l.e & l.m.n[n] -> _r.n[n]-
                ~r.e & ~l.e -> _r.n[n]+

                _r.n[n] => r.m.n[n]-
            )

            (| : n : N : ~_r.n[n]) => vr+
            (| : n : N : l.m.n[n]) => _vl-
            _vl => vl-

            g.pReset | vr & vl -> l.e-
            ~g.sReset & ~vr & ~vl -> l.e+
        }
    }
    export
    template <pint N>
    defproc WCHB (globals g; e1of<N> l, r) {
        // weak-condition logic half buffer
        c1of<N> _r;
        bool _le;
        prs<g.Vdd, g.GND> {
            (: n : N :
                g._sReset & r.e & l.m.n[n] -> _r.n[n]-
                ~g._pReset | ~r.e & ~l.m.n[n] -> _r.n[n]+

                _r.n[n] => r.m.n[n]-
            )

            (& : n : N : _r.n[n]) => _le-
            _le => l.e-
        }
    }
    namespace test {
        export
        template <pint N, M>
        defproc TEST_PCHB (globals g) {
            passive_handshaker<1> s(,);
            active_handshaker<1> r(g,,);
            PCHB<N> pchb[M];
            (; m : M : pchb[m].g = g;)

            pchb[0].l.m.n[0] = s.o[0];
            (; n : 1..N-1 : pchb[0].l.m.n[n] = g.GND;)
            pchb[0].l.e = s.i[0];

            pchb[M-1].r.m.n[0] = r.i[0];
            pchb[M-1].r.e = r.o[0];

            (; m : M-1 :
                pchb[m].r = pchb[m+1].l;
            )
        }
        export
        template <pint N, M>
        defproc TEST_WCHB (globals g) {
            passive_handshaker<1> s(,);
            active_handshaker<1> r(g,,);
            WCHB<N> wchb[M];
            (; m : M : wchb[m].g = g;)

            wchb[0].l.m.n[0] = s.o[0];
            (; n : 1..N-1 : wchb[0].l.m.n[n] = g.GND;)
            wchb[0].l.e = s.i[0];

            wchb[M-1].r.m.n[0] = r.i[0];
            wchb[M-1].r.e = r.o[0];

            (; m : M-1 :
                wchb[m].r = wchb[m+1].l;
            )
        }
    }
}
